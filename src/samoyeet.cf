-- programs ------------------------------------------------

entrypoints Program ;

Program.   Program ::= [Stmt];

Arg. 	   Arg ::= SType Ident;

RefArg.	   Arg ::= SType "&" Ident;

separator  Arg "," ;

-- statements ----------------------------------------------

Block.     Block ::= "{" [Stmt] "}" ;

separator  Stmt "" ;

Empty.     Stmt ::= ";" ;

BStmt.     Stmt ::= Block ;

Decl.      Stmt ::= SType [Item] ";" ;

NoInit.    Item ::= Ident ; 

Init.      Item ::= Ident "=" Expr ;

separator nonempty Item "," ;

Ass.       Stmt ::= Ident "=" Expr  ";" ;

Incr.      Stmt ::= Ident "++"  ";" ;

Decr.      Stmt ::= Ident "--"  ";" ;

Ret.       Stmt ::= "return" Expr ";" ;

VRet.      Stmt ::= "return" ";" ;

Cond.      Stmt ::= "if" "(" Expr ")" Stmt  ;

CondElse.  Stmt ::= "if" "(" Expr ")" Stmt "else" Stmt ;

While.     Stmt ::= "while" "(" Expr ")" Stmt ;

SBreak.    Stmt ::= "break;" ;

SContinue. Stmt ::= "continue;" ;

SExp.      Stmt ::= Expr  ";" ;

For.       Stmt ::= "for" Ident "from" Expr "to" Expr "do" Stmt ;

Print.     Stmt ::= "yeet" Expr ";" ;

SFnDef.	   Stmt ::= SType Ident "(" [Arg] ")" Block ;


-- Types ---------------------------------------------------

Int.      SType ::= "int" ;

Str.      SType ::= "string" ;

Bool.     SType ::= "boolean" ;

Void.     SType ::= "void" ;

Fun.       SType ::= "Fun" "<" SType "(" [MaybeRefType] ")" ">" ;

NoRef.     MaybeRefType ::= SType ;

JustRef.   MaybeRefType ::= SType "&" ;

separator  MaybeRefType "," ;

separator  SType "," ;


-- Expressions ---------------------------------------------

EVar.      Expr6 ::= Ident ;

ELitInt.   Expr6 ::= Integer ;

ELitTrue.  Expr6 ::= "true" ;

ELitFalse. Expr6 ::= "false" ;

EApp.      Expr6 ::= Ident "(" [Expr] ")" ;

EString.   Expr6 ::= String ;

Neg.       Expr5 ::= "-" Expr6 ;

Not.       Expr5 ::= "!" Expr6 ;

EMul.      Expr4 ::= Expr4 MulOp Expr5 ;

EAdd.      Expr3 ::= Expr3 AddOp Expr4 ;

ERel.      Expr2 ::= Expr2 RelOp Expr3 ;

EAnd.      Expr1 ::= Expr2 "and" Expr1 ;

EOr.       Expr ::= Expr1 "or" Expr ;

ELambda.   Expr  ::= "[]" "(" [Arg] ")" ":" SType Block;

coercions  Expr 6 ;

separator  Expr "," ;

-- operators -----------------------------------------------

Plus.      AddOp ::= "+" ;

Minus.     AddOp ::= "-" ;

Times.     MulOp ::= "*" ;

Div.       MulOp ::= "/" ;

Mod.       MulOp ::= "%" ;

LTH.       RelOp ::= "<" ;

LE.        RelOp ::= "<=" ;

GTH.       RelOp ::= ">" ;

GE.        RelOp ::= ">=" ;

EQU.       RelOp ::= "==" ;

NE.        RelOp ::= "!=" ;

-- comments ------------------------------------------------

comment    "#" ;

comment    "//" ;

comment    "/*" "*/" ;
